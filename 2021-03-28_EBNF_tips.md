# EBNF tips

EBNF についての tips をまとめます。

## 空白を無視する構文を作る

空白や改行を無視する処理は、字句解析において行われることが多いでしょう。しかし、インデント構文があるとそうもいきません。 EBNF で空白を無視する構文を作りたい時の、経験を元にした tips です。

```ebnf
expression = spaces and breaks , matrix;
matrix = [ row , { spaces and breaks , row } , [ spaces and breaks ] ];
row = "(" , spaces , [ natural number , spaces , { "," , spaces , natural number , spaces } , [ "," , spaces ] ] , ")";
natural number = "0" | non-zero digit , { digit };
non-zero digit = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
digit ="0" |  "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
spaces and breaks = { space and break };
spaces = { space };
space and break = space | break;
break = "\n" , ["\r"] | "\r";
space = " " | "\t";
```

このような構文が、空白を無視する構文の実例です。これには以下のようなテクニックが含まれています。

1. 無視可能な空白を表現する非終端記号を両脇に入れない。たとえば、 `row = "(" , spaces , [ natural number , spaces , { "," , spaces , natural number , spaces } , [ "," , spaces ] ] , ")";` を `row = spaces , "(" , spaces , [ natural number , spaces , { "," , spaces , natural number , spaces } , [ "," , spaces ] ] , ")" , spaces;`としない。
2. 無視可能な空白を表現する非終端記号と、そうでない非終端記号が交互に現れるようにする。たとえば、 `row = "(" , spaces , [ natural number , spaces , { "," , spaces , natural number , spaces } , [ "," , spaces ] ] , ")";` から特殊な記号を取り除くと `row = "(" , spaces , natural number , spaces , "," , spaces , natural number , spaces , "," , spaces , ")";` となり、交互に出現している。また、角括弧 `[ ]` と波括弧 `{ }` の中でもそうである。

これらのテクニックは、構文木の最上部においては無視して構いません。
